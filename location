ruleset location {
    meta {
        name "location"
        author "Riley Monson"
        logging off
        use module a169x701 alias CloudRain
        use module a41x186  alias SquareTag
        use module b505386x5 alias loc
    }
    rule nearby {
        select when location new_current
        pre {
            map = loc:get_location_data("checkin");
            
            r90 = math:pi()/2;
            rEm = 3963;
            
            pds_lat = math:deg2rad(map{"lat"});
            pds_long = math:deg2rad(map{"long"});
            new_lat = math:deg2rad(event:attr("lat"));
            new_long = math:deg2rad(event:attr("long"));
            
            dist = math:great_circle_distance(pds_long,r90 - pds_lat, new_long,r90 - new_lat, rEm);
        }
        fired {
            set ent:dist dist; 
            raise explicit event 'location_nearby' attributes {"distance" : dist} if (dist < 5);
            raise explicit event 'location_far' attributes {"distance" : dist} if (dist > 5);
        }
    }
    rule display {
        select when web cloudAppSelected
        pre {
            my_html = <<
              <div id="content">
                Distance:#{ent:dist}
              </div>
            >>;
        }
        {
            SquareTag:inject_styling();
            CloudRain:createLoadPanel("Foursquare", {}, my_html);
        }
    }
}
